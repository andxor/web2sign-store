plugins {
    id 'war'
    id 'com.palantir.git-version' version '0.12.2'
}

group = 'com.andxor'
version gitVersion()

compileJava {
    if (JavaVersion.current() <= JavaVersion.VERSION_1_8) {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    } else
        options.release = 8
    options.encoding = 'UTF-8'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'src'
            include 'logback.xml'
            include 'config.json'
        }
    }
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy.eachDependency {
        if (it.requested.name == 'commons-logging')
            it.useTarget 'org.slf4j:jcl-over-slf4j:1.7.+'
    }
}

dependencies {
    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.+', {
        exclude group: 'com.sun.mail'
    }
}

war {
    webXml = file('etc/web.xml')
    from('WebContent')
}

task writeVersion {
    doLast {
        def b = versionDetails()
        def date = java.time.Instant.now()
        file('WebContent/version.txt').text = "Revision: ${b.lastTag}-${b.commitDistance}-${b.gitHash}\nBuild: ${date}\nCommit: ${b.gitHashFull}\nBranch: ${b.branchName}\n"
    }
}
tasks.war.dependsOn writeVersion
